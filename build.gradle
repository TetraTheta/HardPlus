//file:noinspection SpellCheckingInspection

// Required Environment Variables:
// MODRINTH_TOKEN : Token for Modrinth Publish

plugins {
  id("java")
  id("co.uzzu.dotenv.gradle") version "4.0.0"
  id("com.modrinth.minotaur") version "2.+"
  // No Hangar publish. I'm fed up with their not supporting Groovy DSL.
}

group = "io.github.tetratheta"
version = "3.0.0"

def verMC = "1.21.11" // Minecraft (Modrinth, Hangar)
def verAPI = "1.21.11" // API (dependencies & plugin.yml)
def verPaper = "1.21.11-R0.1-SNAPSHOT" // Paper (Paper)
def verWorldGuard = "7.0.15" // WorldGuard
def releaseChannel = "release" // Release Channel (Modrinth & Hangar)

repositories {
  mavenCentral()
  maven {
    url = uri("https://repo.papermc.io/repository/maven-public/")
  }
  maven {
    url = uri("https://oss.sonatype.org/content/groups/public/")
  }
  maven {
    url = uri("https://maven.enginehub.org/repo/")
  }
}

dependencies {
  //noinspection VulnerableLibrariesLocal
  compileOnly("io.papermc.paper:paper-api:$verPaper")
  compileOnly("com.sk89q.worldguard:worldguard-bukkit:$verWorldGuard")
}

def targetJavaVersion = 21

java {
  def javaVersion = JavaVersion.toVersion(targetJavaVersion)
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
  if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = "UTF-8"
  if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
    options.release.set(targetJavaVersion)
  }
}

processResources {
  def props = ["version": version, "api_version": verAPI]
  inputs.properties(props)
  filteringCharset = "UTF-8"
  filesMatching("paper-plugin.yml") {
    expand(props)
  }
}

// Publish
modrinth {
  token = env.fetch("MODRINTH_TOKEN") //System.getenv("MODRINTH_TOKEN")
  projectId = "hardplus"
  versionType = releaseChannel
  uploadFile = jar
  gameVersions = [verMC]
  loaders = ["paper"]
  syncBodyFrom = rootProject.file("README.md").text
}

// Cache
configurations.configureEach {
  resolutionStrategy.cacheChangingModulesFor(7, "days")
}
